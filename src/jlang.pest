program = _{ SOI ~ "\n"* ~ (stmt ~ "\n"+) * ~ stmt ~ EOI }
stmt = _{ expr }
expr = {
      assgmtExpr
    | monadicExpr
    | dyadicExpr
    | string
    | terms
}

assgmtExpr = { verb ~ expr }
dyadicExpr = { (monadicExpr | terms) ~ verb ~ expr }
monadicExpr = { ident ~ "=:" ~ expr }
terms = { term+ }
term = _{ decimal | integer | ident | "(" ~ expr ~ ")" }
verb = {
    ">:" | "*:" | "-"  | "%" | "#" | ">."
  | "+"  | "*"  | "<"  | "=" | "^" | "|"
  | ">"  | "$"
}

integer = @{ "_"? ~ ASCII_DIGIT+ }
decimal = @{ "_"? ~ ASCII_DIGIT+  ~ "." ~ ASCII_DIGIT* }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
string = @{ "'" ~ ( "''" | (!"'" ~ ANY) )* ~ "'" }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }